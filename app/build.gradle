apply plugin: 'com.android.application'

def getVersionValues() {
    def cmd = "git describe --tags"
    Process process = cmd.execute()
    String tagsDescription = process.in.text

    return tagsDescription.split('-')
}

def getVersionName() {
    def versionValues = getVersionValues()

    if (versionValues.size() == 3) {
        return "${versionValues[0]}.${versionValues[1]}"
    }
    else {
        return "${versionValues[0]}.0"
    }
}

def getVersionCode() {
    def cmd = "git rev-list --tags --count"
    Process process = cmd.execute()

    return process.in.text.toInteger()
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.example.helloworld"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode = this.getVersionCode()
        versionName = this.getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
